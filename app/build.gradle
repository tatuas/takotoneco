plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

File debugSignature = rootProject.file("signatures/debug.gradle")
File releaseSignature = rootProject.file("signatures/release.gradle")

apply from: debugSignature, to: android
if (releaseSignature.exists()) apply from: releaseSignature, to: android

android {
    compileSdk 32
    buildToolsVersion '32.0.0'

    defaultConfig {
        applicationId app_id

        versionCode app_version_code
        versionName app_version_name

        minSdk 24
        targetSdk 32

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'environment'

    productFlavors {
        develop {
            dimension 'environment'
            applicationId app_id + '.develop'
            signingConfig signingConfigs.debug
        }

        product {
            dimension 'environment'
            applicationId app_id
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        all {
            java.srcDir("src/$name/kotlin")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // coil
    implementation 'io.coil-kt:coil:1.4.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    // timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // okhttp
    def okhttp_version = '4.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // kotlinx
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    // dagger hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}